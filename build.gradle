plugins {
    alias(libs.plugins.java)
    alias(libs.plugins.quarkus)
    alias(libs.plugins.maven.publish)
    id 'signing'
    id 'com.gradleup.nmcp' version '1.2.0'
}
def pipelineBomVersion = findProperty('pipelineBomVersion') ?: '0.1.2-SNAPSHOT'

dependencies {
    implementation 'io.quarkus:quarkus-container-image-docker'
    // Import pipeline BOM to align all managed versions
    implementation platform("ai.pipestream:pipeline-bom:${pipelineBomVersion}")

    // Core Quarkus dependencies
    implementation libs.quarkus.arc
    implementation libs.quarkus.rest
    implementation libs.quarkus.grpc
    implementation 'io.quarkus:quarkus-grpc'
    implementation 'io.quarkus:quarkus-arc'
    implementation 'io.quarkus:quarkus-smallrye-health'
    implementation 'io.quarkus:quarkus-smallrye-stork'
    
    // REST API dependencies
    implementation 'io.quarkus:quarkus-rest'
    implementation 'io.quarkus:quarkus-rest-jackson'
    implementation 'io.quarkus:quarkus-smallrye-openapi'
    implementation 'io.quarkus:quarkus-hibernate-validator'
    implementation 'io.quarkus:quarkus-cache'

    // Service Discovery dependencies for Consul
    implementation 'io.smallrye.stork:stork-service-discovery-consul'
    implementation 'io.smallrye.reactive:smallrye-mutiny-vertx-consul-client'

    testImplementation 'io.quarkus:quarkus-junit5'
    testImplementation 'io.rest-assured:rest-assured'

    testImplementation libs.pipestream.grpc.wiremock

    // Test data dependencies - sample documents published as Maven artifacts
    testImplementation 'ai.pipestream.testdata:sample-doc-types:0.1.0-SNAPSHOT'
    testImplementation 'ai.pipestream.testdata:test-documents:0.1.0-SNAPSHOT'
    // Pipestream libraries - versions managed entirely by the BOM/catalog
    implementation libs.pipestream.compose.devservices
    implementation libs.pipestream.grpc.stubs
    implementation libs.pipestream.commons
    implementation libs.pipestream.dynamic.grpc
    implementation libs.pipestream.dynamic.grpc.registration.clients


    // Apache Commons IO for file operations
    testImplementation libs.commons.io
    // Apache Commons Compress for reading from JARs/ZIPs
    testImplementation libs.commons.compress
    testImplementation("io.quarkus:quarkus-junit5-mockito")
    // Apache Tika dependencies - use standard package which includes most parsers
    implementation libs.tika.core
    implementation libs.tika.parsers.standard
    // Extended parsers for better content extraction
    implementation libs.tika.parser.scientific
    // OCR support for images and scanned documents (requires tesseract)
    implementation libs.tika.parser.ocr
    // Markdown parsing (commonmark)
    implementation libs.commonmark
    // PDF outline extraction (pdfbox)
    implementation libs.pdfbox
    runtimeOnly libs.quarkus.logging.manager


}

group = 'ai.pipestream.module'
version = "${pipelineBomVersion}"

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

test {
    systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
    maxHeapSize = "10g"
    jvmArgs = ["-Xmx10g", "-XX:MaxMetaspaceSize=1g", "--add-opens", "java.base/java.lang=ALL-UNNAMED"]

    // Configure test document paths with fallback logic
    // Priority: 1) System property (-D), 2) Environment variable, 3) Auto-detected relative path

    // Test documents path (for general document testing)
    def testDocsProp = System.getProperty("test.documents")
    def testDocsEnv = System.getenv("TEST_DOCUMENTS")
    def testDocsDefault = "${project.rootDir}/../../sample-documents/test-documents"

    def testDocsPath = testDocsProp ?: testDocsEnv ?: testDocsDefault
    systemProperty "test.documents", testDocsPath
    println "[TEST CONFIG] test.documents=${testDocsPath}"

    // Sample document types path (for comprehensive document type testing)
    def sampleDocTypesProp = System.getProperty("sample.doc.types")
    def sampleDocTypesEnv = System.getenv("SAMPLE_DOC_TYPES")
    def sampleDocTypesDefault = "${project.rootDir}/../../sample-documents/sample_doc_types"

    def sampleDocTypesPath = sampleDocTypesProp ?: sampleDocTypesEnv ?: sampleDocTypesDefault
    systemProperty "sample.doc.types", sampleDocTypesPath
    println "[TEST CONFIG] sample.doc.types=${sampleDocTypesPath}"

    // Validate that the configured paths exist
    def testDocsFile = new File(testDocsPath)
    def sampleDocTypesFile = new File(sampleDocTypesPath)

    if (!testDocsFile.exists()) {
        println "[TEST CONFIG WARNING] test.documents path does not exist: ${testDocsPath}"
    }
    if (!sampleDocTypesFile.exists()) {
        println "[TEST CONFIG WARNING] sample.doc.types path does not exist: ${sampleDocTypesPath}"
    }
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

quarkusDev {
    jvmArgs = ["-Xmx10g", "-XX:MaxMetaspaceSize=1g", "--add-opens", "java.base/java.lang=ALL-UNNAMED"]
}

// Publishing configuration
publishing {
    publications {
        //noinspection GroovyAssignabilityCheck
        maven(MavenPublication) { publication ->
            from components.java
            //noinspection GroovyAssignabilityCheck
            artifact(file("${buildDir}/quarkus-app/quarkus-run.jar")) { artifact ->
                artifact.classifier = 'runner'
                artifact.builtBy tasks.named('quarkusBuild')
            }
            pom { pom ->
                name.set('Parser Module')
                description.set('Tika Parkser Module for the pipeline system')
                url.set('https://github.com/ai-pipestream/module-parser')

                licenses {
                    license {
                        name.set('MIT License')
                        url.set('https://opensource.org/licenses/MIT')
                    }
                }

                developers {
                    developer {
                        id.set('krickert')
                        name.set('Pipestream Platform Team')
                    }
                }

                scm {
                    connection.set('scm:git:git://github.com/ai-pipestream/module-parser.git')
                    developerConnection.set('scm:git:ssh://github.com/ai-pipestream/module-parser.git')
                    url.set('https://github.com/ai-pipestream/module-parser')
                }
            }
        }
    }

    repositories {
        // Publish to Maven Local for development
        mavenLocal()

        // Publish to GitHub Packages when credentials are available (e.g. CI)
        def githubToken = providers.environmentVariable("GITHUB_TOKEN")
        if (githubToken.isPresent()) {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/ai-pipestream/module-parser")
                credentials {
                    username = providers.environmentVariable("GITHUB_ACTOR").getOrElse("github-actions")
                    password = githubToken.get()
                }
                mavenContent {
                    releasesOnly()
                }
            }
        }
    }
}

// Signing configuration for Maven Central
signing {
    def signingKey = System.getenv("GPG_SIGNING_KEY")
    def signingPassword = System.getenv("GPG_SIGNING_PASSPHRASE")

    if (signingKey && signingPassword) {
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.maven
    }
}

// Maven Central publishing via nmcp plugin
nmcp {
    publishAllPublicationsToCentralPortal {
        username = providers.environmentVariable("MAVEN_CENTRAL_USERNAME")
        password = providers.environmentVariable("MAVEN_CENTRAL_PASSWORD")
        publishingType = "AUTOMATIC"
    }
}