pluginManagement {
    repositories {
        gradlePluginPortal()
    }
}

rootProject.name = 'module-parser'

dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.PREFER_SETTINGS)

    versionCatalogs {
        libs {
            // Import version catalog from published BOM
            from("ai.pipestream:pipestream-bom-catalog:${settings.gradle.startParameter.projectProperties['pipestreamBomVersion'] ?: '0.7.19-SNAPSHOT'}")
        }
    }

    repositories {
        // Maven Local for published ai.pipestream artifacts during development
        mavenLocal {
            content {
                includeGroupByRegex "ai\\.pipestream(\\..*)?"
            }
        }

        // Maven Central (always available, default for public builds)
        mavenCentral()

        // Maven Central snapshots host ai.pipestream SNAPSHOTs
        maven {
            url = uri('https://central.sonatype.com/repository/maven-snapshots/')
            mavenContent {
                snapshotsOnly()
            }
            content {
                includeGroupByRegex "ai\\.pipestream(\\..*)?"
            }
        }

        // Apache snapshots for Tika
        maven {
            url = uri("https://repository.apache.org/content/repositories/snapshots/")
            content {
                includeGroup "org.apache.tika"
            }
            mavenContent {
                snapshotsOnly()
            }
        }
    }
}
