pluginManagement {
    repositories {
        gradlePluginPortal()
    }
}

rootProject.name = 'module-parser'

dependencyResolutionManagement {
//    repositoriesMode.set(RepositoriesMode.PREFER_SETTINGS)

    versionCatalogs {
        libs {
            // Import version catalog from published BOM
            // Available on both private Reposilite and public GitHub Packages
            from("ai.pipestream:pipeline-bom-catalog:0.1.2-SNAPSHOT")
        }
    }

    repositories {
        // Maven Local for published Pipeline artifacts during development
        mavenLocal {
            content {
                includeGroupByRegex "ai\\.pipestream(\\..*)?"
            }
        }

        // Maven Central (always available, default for public builds)
        mavenCentral()

        // GitHub Packages for ai.pipestream artifacts
        maven {
            url = uri("https://maven.pkg.github.com/ai-pipestream/pipeline-bom")
            credentials {
                username = providers.systemProperty("gpr.user").orElse(providers.environmentVariable("GITHUB_ACTOR")).orNull
                password = providers.systemProperty("gpr.token").orElse(providers.environmentVariable("GITHUB_TOKEN")).orNull
            }
            content {
                includeGroupByRegex "ai\\.pipestream(\\..*)?"
            }
        }

        // Maven Central Snapshots for consuming published SNAPSHOTs
        maven {
            url = uri('https://oss.sonatype.org/content/repositories/snapshots/')
            mavenContent {
                snapshotsOnly()
            }
        }
        // Apache snapshots for Tika
        maven {
            url = uri("https://repository.apache.org/content/repositories/snapshots/")
            content {
                includeGroup "org.apache.tika"
            }
            mavenContent {
                snapshotsOnly()
            }
        }

//        // Apache snapshots for Tika 4.x pre-release (default for public builds)
//        if (System.getenv('CI_ENV') != 'internal') {
//            maven {
//                url = uri('https://repository.apache.org/content/repositories/snapshots/')
//                allowInsecureProtocol = false
//                content {
//                    includeGroupByRegex "org\\.apache\\.tika(\\..*)?"
//                }
//            }
//        }
    }
}
